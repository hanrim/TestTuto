apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.sktelecom.tracker.android'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'file:' + projectDir + '/tracker' }     //tracker
    }
    dependencies {
        // apt
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        // retro-lambda : 3.2.5 에서 빌드가 안되서 3.2.4 사용. 추후 3.2.6 이상 업데이트시 확인 필요.
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
        // fabric
        classpath 'io.fabric.tools:gradle:1.+'
        // dexcount
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.4.2'
        //tracker
//        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath 'com.sktelecom.tracker.android:plugin:8.4.4'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    flatDir{
        dirs 'libs'
    }
    maven { url 'file:' + projectDir + '/tracker' }     //tracker
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods true
    incremental true
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId 'com.sktelecom.tdrive'
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 10000
        versionName '1.0.0'
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
    }
    signingConfigs {
        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('etc/tdrive-debug-keystore.jks')
            storePassword 'android'
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('etc/tdrive-debug-keystore.jks')
            storePassword 'android'
        }
    }
    buildTypes {
        release {
            buildConfigField('Integer', 'CONTAINER_RESOURCE', 'R.raw.gtm_release')
            buildConfigField('String', 'SERVICE_URL', '".tdrive.sktelecom.com"')
            buildConfigField('String', 'CONTAINER_ID', '"GTM-NVCRQT"')
            buildConfigField('String', 'MIXPANEL_TOKEN', '"5d56666c5c472540f38604d4e760fbb1"')
            resValue('bool', 'tracker_log', 'false')

            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            buildConfigField('Integer', 'CONTAINER_RESOURCE', 'R.raw.gtm_debug')
            buildConfigField('String', 'SERVICE_URL', '".tdrive.sktelecom.com"')
            buildConfigField('String', 'CONTAINER_ID', '"GTM-5938N6"')
            buildConfigField('String', 'MIXPANEL_TOKEN', '"50f71dba845125bc7ed816bf1b69a18c"')
            resValue('bool', 'tracker_log', 'true')

            signingConfig signingConfigs.debug
            minifyEnabled false
//            versionNameSuffix '-dev'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets.main {
        jniLibs.srcDirs 'jniLibs'
        jni.srcDirs = []
    }
    useLibrary  'org.apache.http.legacy'
}

dependencies {
    // local file
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // app-compat
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    // gson
    compile 'com.google.code.gson:gson:2.6.2'
    // okhttp3
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    // butterknife
    compile 'com.jakewharton:butterknife:7.0.1'
    apt 'com.jakewharton:butterknife:7.0.1'
    // timber
    compile 'com.jakewharton.timber:timber:4.1.1'
    // stetho
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    provided 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    provided 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-timber:1.3.1@aar'
    provided 'com.facebook.stetho:stetho-timber:1.3.1@jar'
//    debugCompile 'com.facebook.stetho:stetho-js-rhino:1.3.1'
//    provided 'com.facebook.stetho:stetho-js-rhino:1.3.1'
    // fabric
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    // test
    testCompile 'junit:junit:4.12'
    // retro-lambda
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:+'
    // Pull To Refresh(버전 변경시, pull_to_refresh_header_vertical.xml을 수정해야함.)
    compile 'com.loopeer.android.thirdparty.pulltorefresh:Android-PullToRefresh:2.1.1'
    //apache http
    compile 'org.apache.httpcomponents:httpcore:4.4.1'
    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile(name:'ssolib', ext:'aar')   // dependencies 추가
}
